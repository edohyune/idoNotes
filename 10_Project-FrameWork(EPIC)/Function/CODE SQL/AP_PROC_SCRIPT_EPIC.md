---
CDT: 2024-03-28 07:13
Discriptions: 저장장소 위치를 생성하고 코드 수정 D:\DATABASE\EPIC\SP_BACK\
---
---
```SQL
USE [master]
GO
/****** Object:  StoredProcedure [dbo].[AP_PROC_SCRIPT_EPIC]    Script Date: 2023-10-07 오후 1:55:43 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROC [dbo].[AP_PROC_SCRIPT_EPIC] 
 @TARGET_INSTANCE_IP_PORT NVARCHAR(4000) = '.'
,@TARGET_DB_NAME NVARCHAR(4000) = 'EPIC'
,@SCHEMA_NAME NVARCHAR(4000) = 'DBO'
,@TARGET_SVR_FOLDER NVARCHAR(4000) = 'D:\DATABASE\EPIC\SP_BACK\'
,@PURGE_TIMES INT = 10
,@ENABLP_XP_CMDSHELL_FORCE INT = 1
,@CREATE_TARGET_FOLDER_FORCE INT = 1
,@SLEEP_TIME VARCHAR(100) ='00:00:00.000'
AS
SET NOCOUNT ON 
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED 

-- SCRIPT BY MINSOUK KIM
-- MSSQL MVP, 2006~2009 
-- VERSION 1.20110511
-- CAFE, WWW.SQLTAG.ORG, CAFE.NAVER.COM/SQLMVP
-- BLOG, SQLSQL.TISTORY.COM
-- TEL, 01099670955
-- MAIL, MINSOUK@HOTMAIL.COM

DECLARE @MAX_CNT INT 
,@CUR_CNT INT 
,@NAME NVARCHAR(2000)
,@SCHEMA_ID INT 
,@BCP_QUERY NVARCHAR(4000)
,@OBJECT_LIST_QUERY NVARCHAR(4000)
,@BCP_CMD NVARCHAR(4000)
,@INSTANCE_NAME NVARCHAR(1000)
,@XP_CMDSHELL_STATUS INT 
,@XP_CMDSHELL_CHANGE_STATUS INT 
,@TODAY NVARCHAR(4000) 
,@CMD_STRING NVARCHAR(4000) 
,@PURGE_CUR_IDX INT
,@PURGE_MAX_IDX INT
,@PURGE_TARGET_FOLDER_NAME NVARCHAR(1000)
,@SHOW_ADVANCED_OPTIONS_STATUS INT 
,@SHOW_ADVANCED_OPTIONS_CHANGE_STATUS INT 


DECLARE @TBL_PURGE_TARGET_FOLDER_LIST TABLE (IDX INT IDENTITY(1,1),PURGE_TARGET_FOLDER_NAME NVARCHAR(1000))

IF OBJECT_ID ('TEMPDB..\#TBL_TOTEXT_LIST') IS NOT NULL
DROP TABLE \#TBL_TOTEXT_LIST
CREATE TABLE \#TBL_TOTEXT_LIST (IDX INT IDENTITY(1,1), NAME NVARCHAR(2000), SCHEMA_ID INT)

SELECT @TODAY = CONVERT(VARCHAR(8), GETDATE(), 112)+'_'+REPLACE(CONVERT(VARCHAR(20), GETDATE(), 114), ':', '')
	 , @INSTANCE_NAME = REPLACE(@@SERVERNAME , '\','_')
SELECT @XP_CMDSHELL_STATUS = CAST(VALUE_IN_USE AS INT) FROM SYS.CONFIGURATIONS WHERE NAME ='XP_CMDSHELL'
SELECT @SHOW_ADVANCED_OPTIONS_STATUS = CAST(VALUE_IN_USE AS INT) FROM SYS.CONFIGURATIONS WHERE NAME ='SHOW ADVANCED OPTIONS'

IF @SHOW_ADVANCED_OPTIONS_STATUS = 0 BEGIN 
	EXEC SP_CONFIGURE 'SHOW ADVANCED OPTIONS', 1
	RECONFIGURE WITH OVERRIDE 
	SET @SHOW_ADVANCED_OPTIONS_CHANGE_STATUS = 1
END 


IF @XP_CMDSHELL_STATUS = 0 AND @ENABLP_XP_CMDSHELL_FORCE = 0 BEGIN 
	SELECT 'CHANGE @ENABLP_XP_CMDSHELL_FORCE PARAMETER TO 1'
	RETURN 0 
END 

IF @ENABLP_XP_CMDSHELL_FORCE = 1 AND @XP_CMDSHELL_STATUS = 0 BEGIN 
	EXEC SP_CONFIGURE 'XP_CMDSHELL', 1 
	RECONFIGURE WITH OVERRIDE 
	SET @XP_CMDSHELL_CHANGE_STATUS = 1 
END 

IF @CREATE_TARGET_FOLDER_FORCE = 1 BEGIN 
	SET @CMD_STRING ='MKDIR '+@TARGET_SVR_FOLDER+@INSTANCE_NAME+'\'+@TARGET_DB_NAME+'\'+@TODAY 
	EXEC MASTER.DBO.XP_CMDSHELL @CMD_STRING, NO_OUTPUT
END 

IF @PURGE_TIMES <> 0 BEGIN
	SET @CMD_STRING ='DIR '+@TARGET_SVR_FOLDER+@INSTANCE_NAME+'\'+@TARGET_DB_NAME+' /b /o:-d /a:d'
	INSERT INTO @TBL_PURGE_TARGET_FOLDER_LIST
	EXEC MASTER.DBO.XP_CMDSHELL @CMD_STRING
	DELETE @TBL_PURGE_TARGET_FOLDER_LIST WHERE IDX <= @PURGE_TIMES OR PURGE_TARGET_FOLDER_NAME IS NULL
	SELECT @PURGE_CUR_IDX = ISNULL(MIN(IDX),0), @PURGE_MAX_IDX = ISNULL(MAX(IDX),0) FROM @TBL_PURGE_TARGET_FOLDER_LIST
	WHILE (1=1) BEGIN 
		SELECT @PURGE_TARGET_FOLDER_NAME = PURGE_TARGET_FOLDER_NAME FROM @TBL_PURGE_TARGET_FOLDER_LIST WHERE IDX = @PURGE_CUR_IDX
		SET @CMD_STRING ='RMDIR '+@TARGET_SVR_FOLDER+@INSTANCE_NAME+'\'+@TARGET_DB_NAME+'\'+@PURGE_TARGET_FOLDER_NAME + ' /S /Q'
		EXEC MASTER.DBO.XP_CMDSHELL @CMD_STRING, NO_OUTPUT
		SET @PURGE_CUR_IDX = @PURGE_CUR_IDX + 1 
		IF @PURGE_CUR_IDX > @PURGE_MAX_IDX 
			BREAK; 
	END 
END 

SET @OBJECT_LIST_QUERY ='
INSERT INTO \#TBL_TOTEXT_LIST 
SELECT NAME, SCHEMA_ID
  FROM '+@TARGET_DB_NAME+'.SYS.ALL_OBJECTS SP
 WHERE (SP.TYPE = N''P'' OR SP.TYPE = N''RF'' OR SP.TYPE=''PC'' OR SP.TYPE=''V'' OR SP.TYPE=''FN'')
   AND SCHEMA_NAME(SP.SCHEMA_ID)='''+@SCHEMA_NAME+'''' + ' OPTION(RECOMPILE)'

EXEC (@OBJECT_LIST_QUERY)
SELECT @MAX_CNT = @@ROWCOUNT , @CUR_CNT = @@ROWCOUNT 

WHILE (1=1) BEGIN
	BEGIN TRY 
		SELECT @NAME = NAME, @SCHEMA_ID = SCHEMA_ID FROM \#TBL_TOTEXT_LIST WHERE IDX = @CUR_CNT 
		SET @CUR_CNT = @CUR_CNT- 1 
		SET @BCP_QUERY = 'SELECT ISNULL(SMSP.DEFINITION, SSMSP.DEFINITION) AS [DEFINITION] FROM '+@TARGET_DB_NAME+'.SYS.ALL_OBJECTS AS SP LEFT OUTER JOIN '+@TARGET_DB_NAME+'.SYS.SQL_MODULES AS SMSP ON SMSP.OBJECT_ID = SP.OBJECT_ID LEFT OUTER JOIN '+@TARGET_DB_NAME+'.SYS.SYSTEM_SQL_MODULES AS SSMSP ON SSMSP.OBJECT_ID = SP.OBJECT_ID WHERE (SP.TYPE = N''P'' OR SP.TYPE = N''RF'' OR SP.TYPE=''PC'' OR SP.TYPE=''V'' OR SP.TYPE=''FN'') AND(SP.NAME=N'''   +@name+  ''' AND SP.SCHEMA_ID = '+cast(@schema_id as nvarchar(4000))+') OPTION (RECOMPILE)'
		SET @BCP_CMD = 'BCP "'+@BCP_QUERY+'" QUERYOUT '+@TARGET_SVR_FOLDER+@INSTANCE_NAME+'\'+@TARGET_DB_NAME+'\'+@TODAY+'\'+@SCHEMA_NAME+'.'+@NAME+'.TXT -c -T -S'+@TARGET_INSTANCE_IP_PORT
		EXEC MASTER.DBO.XP_CMDSHELL @BCP_CMD, NO_OUTPUT  
		IF @SLEEP_TIME <> '00:00:00.000' BEGIN 
			SET @CMD_STRING ='WAITFOR DELAY '''+@SLEEP_TIME+''''
			EXEC (@CMD_STRING)
		END
	END TRY BEGIN CATCH 
		SELECT 'ERROR!'
	END CATCH 
IF @CUR_CNT <= 0 BREAK
END 

IF @XP_CMDSHELL_CHANGE_STATUS = 1 BEGIN 
	EXEC SP_CONFIGURE 'XP_CMDSHELL', 0
	RECONFIGURE WITH OVERRIDE 
END 

IF @SHOW_ADVANCED_OPTIONS_CHANGE_STATUS = 1 BEGIN 
	EXEC SP_CONFIGURE 'SHOW ADVANCED OPTIONS', 0
	RECONFIGURE WITH OVERRIDE 
END 


SELECT CAST(@MAX_CNT AS VARCHAR(100)) + ' PROCEDURE, FUNCTION, VIEW SCRIPTED!'